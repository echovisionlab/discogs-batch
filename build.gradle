plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group 'io.dsub.discogs'
version '0.1.7'
sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    set('testcontainersVersion', "1.15.3")
    set('springOxmVersion', "5.3.8")
    set('jaxbApiVersion', "2.3.1")
    set('jaxbImplVersion', "3.0.1")
    set('istackCommonsRuntimeVersion', "4.0.1")
    set('classGraphVersion', "4.8.108")
    set('pbVersion', "0.9.1")
    set('discogsJooqVersion', "0.0.3")
    set('liquibaseVersion', "4.4.0")
}

def props = new Properties()
if (file(".env").exists()) {
    file(".env").withInputStream { props.load(it) }
}

repositories {
    mavenCentral()
    maven {
        name = "github-public"
        // Adapt the URL for your remote repository
        url = uri("https://maven.pkg.github.com/state303/discogs-common")
        credentials {
            username = "token"
            password = "\u0067hp_53urXIcnqXhhsAlmIOB3uVhqf7du2135aRbk"
        }
    }
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/state303/discogs-batch")
    }
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

dependencies {
    implementation(
            'com.fasterxml.jackson.datatype:jackson-datatype-jsr310',
            'org.springframework.boot:spring-boot-starter-batch',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            'org.springframework.batch:spring-batch-integration',
            'org.springframework.boot:spring-boot-starter-jooq',
            "io.dsub.discogs:discogs-jooq:${discogsJooqVersion}",
            "org.springframework:spring-oxm:${springOxmVersion}",
            "javax.xml.bind:jaxb-api:${jaxbApiVersion}",
            "com.sun.xml.bind:jaxb-impl:${jaxbImplVersion}",
            "com.sun.istack:istack-commons-runtime:${istackCommonsRuntimeVersion}",
            "io.github.classgraph:classgraph:${classGraphVersion}",
            "me.tongfei:progressbar:${pbVersion}",
            "org.liquibase:liquibase-core:${liquibaseVersion}",
            'mysql:mysql-connector-java',
            'org.postgresql:postgresql',
            'org.projectlombok:lombok',
            'org.apache.commons:commons-lang3'
    )
    testImplementation (
            'org.junit.jupiter:junit-jupiter:5.7.2',
            'org.springframework.boot:spring-boot-starter-test',
            'org.springframework.batch:spring-batch-test',
            'org.mockito:mockito-inline:3.11.1',
            'org.testcontainers:testcontainers',
            'org.testcontainers:postgresql',
            'org.testcontainers:junit-jupiter'
    )
    annotationProcessor (
            'org.springframework.boot:spring-boot-configuration-processor',
            'org.projectlombok:lombok'
    )
    testAnnotationProcessor 'org.projectlombok:lombok'
}

test {
    useJUnitPlatform()
}

// codecov
check.dependsOn jacocoTestReport

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

// publish
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/state303/discogs-batch")
            credentials {
                username = System.getenv('USERNAME')
                password = System.getenv('PKG_TOKEN')
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifactId = 'discogs-batch'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}
bootBuildImage {
    setImageName('sehy0121/discogs-batch-0.1.7')
}
// disable caching
configurations.each {
    it.resolutionStrategy.each {
        it.cacheChangingModulesFor 0, 'seconds'
    }
}
