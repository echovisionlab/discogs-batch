plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'org.springframework.boot' version '2.5.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group 'io.dsub.discogs'
version '0.0.1.1'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

def props = new Properties()
if (file(".env").exists()) {
    file(".env").withInputStream { props.load(it) }
}

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/state303/discogs-batch")
        credentials {
            username = props.getProperty("USERNAME") ?: System.getenv("USERNAME")
            password = props.getProperty("PKG_TOKEN") ?: System.getenv("PKG_TOKEN")
        }
    }
}

dependencies {
    implementation(
            'io.dsub.discogs:discogs-common:0.0.1',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            'org.springframework.boot:spring-boot-starter-batch',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.batch:spring-batch-integration:4.3.3',
            'org.springframework:spring-oxm:5.3.7',
            'javax.xml.bind:jaxb-api:2.3.1',
            'com.sun.xml.bind:jaxb-impl:3.0.1',
            'com.sun.istack:istack-commons-runtime:4.0.1',
            'mysql:mysql-connector-java',
            'org.postgresql:postgresql',
            'me.tongfei:progressbar:0.9.1',
            'org.projectlombok:lombok',
            'org.apache.commons:commons-lang3',
            'org.reflections:reflections:0.9.12',
            'net.ttddyy:datasource-proxy:1.7'
    )
    testImplementation (
            'com.h2database:h2',
            'org.junit.jupiter:junit-jupiter:5.7.2',
            'org.springframework.boot:spring-boot-starter-test',
            'org.springframework.batch:spring-batch-test'
    )

    annotationProcessor (
            'org.springframework.boot:spring-boot-configuration-processor',
            'org.projectlombok:lombok'
    )
    testAnnotationProcessor 'org.projectlombok:lombok'
}

test {
    useJUnitPlatform()
}

// codecov
check.dependsOn jacocoTestReport

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

// publish
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/state303/discogs-batch")
            credentials {
                username = System.getenv('USERNAME')
                password = System.getenv('PKG_TOKEN')
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifactId = 'discogs-batch'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

bootBuildImage {
    setImageName('sehy0121/discogs-batch-0.0.1.1')
}

// disable caching
configurations.each {
    it.resolutionStrategy.each {
        it.cacheChangingModulesFor 0, 'seconds'
    }
}